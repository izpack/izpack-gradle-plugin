plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'jvm-test-suite'
    id 'com.gradle.plugin-publish' version '2.0.0'
}

group = 'com.bmuschko'
version = '3.2'

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle IzPack plugin',
                   'Implementation-Version': archiveVersion,
                   'Built-By': System.getProperty("user.name"),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty("java.version"),
                   'Built-Gradle': gradle.gradleVersion
    }
}

tasks.register('groovydocJar', Jar) {
    dependsOn groovydoc
    archiveClassifier = 'groovydoc'
    from groovydoc.destinationDir
}

testing {
    suites {
        functionalTest(JvmTestSuite) {
            useSpock()

            sources {
                groovy {
                    setSrcDirs(['src/functTest/groovy'])
                }
            }

            dependencies {
                implementation configurations.testRuntimeClasspath
                implementation('org.spockframework:spock-core:2.3-groovy-4.0') {
                    exclude group: 'org.codehaus.groovy'
                }
                implementation('org.junit.jupiter:junit-jupiter-api:5.10.0')
                runtimeOnly('org.junit.jupiter:junit-jupiter-engine:5.10.0')
            }
        }
    }
}

test {
    finalizedBy testing.suites.functionalTest
}

tasks.named("check") {
    dependsOn(testing.suites.functionalTest)
}

gradlePlugin {
    website = "https://github.com/bmuschko/gradle-izpack-plugin"
    vcsUrl = "https://github.com/bmuschko/gradle-izpack-plugin"
    plugins {
        create('izpack-plugin') {
            id = 'com.bmuschko.izpack'
            displayName = 'Gradle IzPack Plugin'
            description = 'Plugin that provides support for packaging applications for the Java platform via IzPack.'
            implementationClass = 'com.bmuschko.gradle.izpack.IzPackPlugin'
            tags = ['packaging', 'distribution', 'installer']
        }
    }
}
